<program>        →  <function list> <instr list>
                 \alt  <function list>
                 \alt  <instr list>

<function list>  → <function list> <function>
                 \alt <function>

<function>       → \lit{sub} identifier <function args>
                   \lit{\{} <instr list> \lit{\}}

<function args>  → \lit{(} <arg list> \lit{)}
                 \alt $\epsilon$

<arg list>       → <arg list> \lit{,} var
                 \alt $\epsilon$

<instr list>     → <instr list> <instr> \lit{;}
                 \alt \lit{\{} <instr list> \lit{\}}
                 \alt <instr> \lit{;}

<funcall>        → \lit{\&} identifier <funcall args> 
                 \alt identifier <funcall args> 

<funcall args>   → \lit{(} <args call list> \lit{)}

<args call list> → <args call list> \lit{,} <instr>
                 \alt <instr>
                 \alt $\epsilon$

<instr>          → <cond>
                 \alt <expr>
                 \alt <expr> \lit{=} <expr>
                 \alt <expr> \lit{if} <expr>
                 \alt <expr> \lit{unless} <expr>
                 \alt \lit{return} <expr>

<cond>           → \lit{if} <expr> \lit{\{} <instr list> \lit{\}} <cond end>
                 \alt \lit{unless} <expr> \lit{\{} <instr list> \lit{\}} <cond end>

<cond end>       → \lit{else} \lit{\{} <instr list> \lit{\}}
                 \alt \lit{elsif} <expr> \lit{\{} <instr list> \lit{\}} <cond end>
                 \alt $\epsilon$

<simple expr>    → <funcall>
                 \alt var
                 \alt integer
                 \alt string

<expr>           → \lit{not} <expr>
                 \alt <expr-or>

<expr-or>        → <expr-or> \lit{||} <expr-and>
                 \alt <expr-and>

<expr-and>       → <expr-and> \lit{\&\&} <expr-eq>
                 \alt <expr-eq>

<expr-eq>        → <comp> \lit{==} <comp>
                 \alt <comp> \lit{!=} <comp>
                 \alt <comp> \lit{eq} <comp>
                 \alt <comp> \lit{ne} <comp>
                 \alt <comp>

<comp>           → <calc> \lit{>} <calc>
                 \alt <calc> '\textless' <calc>
                 \alt <calc> \lit{>=} <calc>
                 \alt <calc> '\textless=' <calc>
                 \alt <calc> \lit{lt} <calc>
                 \alt <calc> \lit{gt} <calc>
                 \alt <calc> \lit{le} <calc>
                 \alt <calc> \lit{ge} <calc>
                 \alt <calc>

<calc>           → <calc> \lit{+} <term>
                 \alt <calc> \lit{-} <term>
                 \alt <calc> \lit{.} <term>
                 \alt <term>

<term>           → <term> \lit{*} <factor>
                 \alt <term> \lit{/} <factor>
                 \alt <factor>

<factor>         → <simple expr>
                 \alt \lit{!} <factor>
                 \alt \lit{+} <factor>
                 \alt \lit{-} <factor>
                 \alt \lit{(} <expr> \lit{)}
