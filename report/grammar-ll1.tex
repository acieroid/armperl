<S>              → <program> \texttt{EOF}

<program>        → <function list> <program'>
                 \alt <instr list>

<program'>       → <instr list>
                 \alt $\epsilon$

<function list>  → <function> <function list'>
                 
<function list'> → <function> <function list'>
		 \alt $\epsilon$

<function>       → \lit{sub} identifier <function args> \lit{\{} <instr list> \lit{\}}

<function args>  → \lit{(} <arg list> \lit{)}
                 \alt $\epsilon$

<arg list>       → var <arg list'>
                 \alt $\epsilon$

<arg list'>      → \lit{,} var <arg list'>
                 \alt $\epsilon$

<instr list>     → <instr> \lit{;} <instr list'>
                 \alt \lit{\{} <instr list> \lit{\}}

<instr list'>    → <instr> \lit{;} <instr list'>
                 \alt $\epsilon$

<funcall>        → \lit{\&} identifier <funcall args>
                 \alt identifier <funcall args>

<funcall args>   → \lit{(} <args call list> \lit{)}

<args call list> → <instr> <args call list'>
                 \alt $\epsilon$

<args call list'> → \lit{,} <instr> <args call list'>
                  \alt $\epsilon$

<instr>          → <cond>
                 \alt <expr> <instr'> 
                 \alt \lit{return} <expr>

<instr'>         → \lit{=} <expr>
                 \alt \lit{if} <expr>
                 \alt \lit{unless} <expr>
                 \alt $\epsilon$

<cond>           → \lit{if} <expr> \lit{\{} <instr list> \lit{\}} <cond end>
                 \alt \lit{unless} <expr> \lit{\{} <instr list> \lit{\}} <cond end>

<cond end>       → \lit{else} \lit{\{} <instr list> \lit{\}}
                 \alt \lit{elsif} <expr> \lit{\{} <instr list> \lit{\}} <cond end>
                 \alt $\epsilon$

<simple expr>    → <funcall>
                 \alt var
                 \alt integer
                 \alt string

<expr>           → \lit{not} <expr>
                 \alt <expr-or>

<expr-or>        → <expr-and> <expr-or'>

<expr-or'>       → \lit{||} <expr-and> <expr-or'>
                 \alt $\epsilon$

<expr-and>       → <expr-eq> <expr-and'>

<expr-and'>      → \lit{\&\&} <expr-eq> <expr-and'>
                 \alt $\epsilon$

<expr-eq>        → <comp> <expr-eq'>

<expr-eq'>       → \lit{==} <comp>
                 \alt \lit{!=} <comp>
                 \alt \lit{eq} <comp>
                 \alt \lit{ne} <comp>
                 \alt  $\epsilon$

<comp>           → <calc> <comp'>

<comp'>          → \lit{>} <calc>
                 \alt \lit{\textless} <calc>
                 \alt \lit{>=} <calc>
                 \alt \lit{\textless=} <calc>
                 \alt \lit{lt} <calc>
                 \alt \lit{gt} <calc>
                 \alt \lit{le} <calc>
                 \alt \lit{ge} <calc>
                 \alt $\epsilon$

<calc>           → <term> <calc'>

<calc'>          → \lit{+} <term> <calc'>
                 \alt \lit{-} <term> <calc'>
                 \alt \lit{.} <term> <calc'>
                 \alt $\epsilon$
                 
<term>           → <factor> <term'>

<term'>          → \lit{*} <factor> <term'>
                 \alt \lit{/} <factor> <term'>
                 \alt $\epsilon$

<factor>         → <simple expr>
                 \alt \lit{!} <factor>
                 \alt \lit{+} <factor>
                 \alt \lit{-} <factor>
                 \alt \lit{(} <expr> \lit{)}
