<program>        → <function list> <program'>
                 | <instr list> <program'>

<program'>       → <function list> <program'>
                 | <instr list> <program'>
		 | ε

<function list>  → <function> <function list'>
                 
<function list'> → <function> <function list'>
		 | ε

<function>       → 'sub' identifier <function args> '{' <instr list> '}'

<function args>  → '(' <arg list> ')'
                 | ε

<arg list>       → var <arg list'>
                 | ε

<arg list'>      → ',' var <arg list'>
                 | ε

<instr list>     →  <instr> <instr list'>
                 | '{' <instr list> '}'

<instr list'>    → ';' <instr> <instr list'>
                 | ';'
                 | ε

<funcall>        → '&' identifier
                 | '&' identifier <funcall args>
                 | identifier
                 | identifier <funcall args>

<funcall args>   → '(' <args call list> ')'
                 | <args call list>
                 | ε

<args call list> → <instr> <args call list'>
                 | ε

<args call list'>→ ',' <instr> <args call list'>
                 | ε

<instr>          → <cond>
                 | <expr>
                 | 'return' <expr>

# ajouter Follow{<cond>} à Follow{<expr>} ?
<cond>           → <expr> 'if' <expr>
                 | <expr> 'unless' <expr>
                 | 'if' <expr> '{' <instr list> '}' <cond end>
                 | 'unless' <expr> '{' <instr list> '}' <cond end>

<cond end>       → 'else' <expr> '{' <instr list> '}' <cond end>
                 | 'elsif' <expr> '{' <instr list> '}' <cond end>
                 | ε

<simple expr>    → <funcall>
                 | var
                 | integer
                 | string

<expr>           → 'not' <expr>
                 | <expr-assign>

<expr-assign>    → <expr-or> '=' <expr-assign>
                 | <expr-or>

<expr-or>        → <expr-and> <expr-or'>

<expr-or'>       → '||' <expr-and> <expr-or'>
                 | ε

<expr-and>       → <expr-eq> <expr-and'>

<expr-and'>      → '&&' <expr-eq> <expr-and'>
                 | ε

<expr-eq>        → <comp> <expr-eq'>

<expr-eq'>       → '==' <expr-eq'>
                 | '!=' <expr-eq'>
                 | 'eq' <expr-eq'>
                 | 'ne' <expr-eq'>
                 |  ε

<comp>           → <calc> <comp'>

<comp'>          → '>' <comp'>
                 | '<' <comp'>
                 | '>=' <comp'>
                 | '<=' <comp'>
                 | 'lt' <comp'>
                 | 'gt' <comp'>
                 | 'le' <comp'>
                 | 'ge' <comp'>
                 | ε

<calc>           → <term> <calc'>

<calc'>          → '+' <term> <calc'>
                 | '-' <term> <calc'>
                 | '.' <term> <calc'>
                 | ε
                 
<term>           → <factor> <term'>

<term'>          → '*' <factor> <term'>
                 | '/' <factor> <term'>
                 | ε

<factor>         → <simple expr>
                 | '(' <expr> ')'
                 | '!' <expr>
                 | '+' <expr>
                 | '-' <expr>
